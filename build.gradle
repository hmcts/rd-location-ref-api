plugins {
  id 'application'
  id 'uk.gov.hmcts.java' version '0.12.65'
  id 'idea'
  id 'jacoco'
  id 'pmd'
  id 'project-report'
  id 'com.github.ben-manes.versions' version '0.52.0'
  id "info.solidsoft.pitest" version '1.15.0'
  id 'io.spring.dependency-management' version '1.1.7'
  id 'org.sonarqube' version '6.0.1.5171'
  id 'org.springframework.boot' version '3.4.4'
  id "org.flywaydb.flyway" version "9.22.3"
  id 'au.com.dius.pact' version '4.6.17'
  id 'org.owasp.dependencycheck' version '11.1.1'
  id 'net.serenity-bdd.serenity-gradle-plugin' version '4.2.22'
}

ext {
  set('springCloudVersion', "2024.0.1")
}

def versions = [
  lombok             : '1.18.38',
  reformLogging      : '6.1.9',
  reformHealthStarter: '0.0.5',
  sonarPitest        : '0.5',
  junitPitest        : '1.2.1',
  commonsLang3       : '3.17.0',
  pact_version       : '4.6.17',
  launchDarklySdk    : "5.10.9",
  junit              : '5.12.2',
  junitPlatform      : '1.12.2',
  jackson            : '2.18.3',
  log4j              : '2.24.3',
  logback            : '1.5.18',
  feign              : '3.8.0',
  bytebuddy          : '1.12.7',
  testContainer_postgresql: '1.20.6',
  serenity           : '4.2.17'
]

tasks.withType(Test) {
  useJUnitPlatform()

  testLogging {
    exceptionFormat = 'full'
  }
}

application {
  mainClass = 'uk.gov.hmcts.reform.lrdapi.LrdApiApplication'

  // this is required to force Java running on the Azure Windows Server OS into using
  // UTF-8 as the default character set instead of windows-1252 which causes issues.
  // this setting only applies when running via gradle bootRun -- see web.config for the
  // java flag that configures the deployed applications
  applicationDefaultJvmArgs = ["-Dfile.encoding=UTF-8"]
}

def jarName = 'rd-location-ref-api.jar'

group = 'uk.gov.hmcts.reform.rdlocation'
version = '0.0.1'

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(21)
  }
}

tasks.withType(JavaExec).configureEach {
  javaLauncher.set(javaToolchains.launcherFor(java.toolchain))
}

jacocoTestCoverageVerification {
  violationRules {
    rule {
      limit {
        counter = 'LINE'
        value = 'COVEREDRATIO'
        minimum = 0.00
      }
    }
  }
}

check.dependsOn jacocoTestCoverageVerification

compileJava {
  options.compilerArgs << '-parameters' << '-Xlint:deprecation' << "-Xlint:unchecked" << "-Werror"
}

compileTestJava {
  options.compilerArgs << '-Xlint:deprecation' << "-Xlint:unchecked" << "-Werror"
}

sourceSets {

  integrationTest {
    java {
      compileClasspath += main.output + test.output
      runtimeClasspath += main.output + test.output
      srcDir file('src/integrationTest/java')
    }
    resources.srcDir file('src/integrationTest/resources')
  }

  functionalTest {
    java {
      compileClasspath += main.output + test.output
      runtimeClasspath += main.output + test.output
      srcDir file('src/functionalTest/java')
    }
    resources.srcDir file('src/functionalTest/resources')
  }

  smokeTest {
    java {
      compileClasspath += main.output + test.output
      runtimeClasspath += main.output + test.output
      srcDir file('src/smokeTest/java')
    }
    resources.srcDir file('src/smokeTest/resources')
  }

  contractTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/contractTest/java')
    }
    resources.srcDir file('src/contractTest/resources')
  }
  pactTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/pactTest/java')
    }
    resources.srcDir file('src/pactTest/resources')
  }

}

idea {
  module {
    testSourceDirs += project.sourceSets.integrationTest.java.srcDirs
    testSourceDirs += project.sourceSets.integrationTest.resources.srcDirs
    testSourceDirs += project.sourceSets.functionalTest.java.srcDirs
    testSourceDirs += project.sourceSets.functionalTest.resources.srcDirs
    testSourceDirs += project.sourceSets.smokeTest.java.srcDirs
    testSourceDirs += project.sourceSets.smokeTest.resources.srcDirs
  }
}

configurations {
  integrationTestImplementation.extendsFrom testCompile
  integrationTestRuntime.extendsFrom testRuntime
  functionalTestImplementation.extendsFrom testCompile
  functionalTestRuntime.extendsFrom testRuntime
  contractTestImplementation.extendsFrom testCompile
  contractTestRuntimeOnly.extendsFrom testRuntime
  pactTestImplementation.extendsFrom testCompile
  pactTestRuntime.extendsFrom testRuntime
}


pmd {
  toolVersion = '6.55.0'
  ignoreFailures = true
  sourceSets = [sourceSets.main, sourceSets.test, sourceSets.functionalTest, sourceSets.integrationTest, sourceSets.smokeTest]
  reportsDir = file("$project.buildDir/reports/pmd")
  ruleSets = [
    'category/java/errorprone.xml',
    'category/java/bestpractices.xml'
  ]
}

task integration(type: Test, description: 'Runs the integration tests.', group: 'Verification') {
  testClassesDirs = sourceSets.integrationTest.output.classesDirs
  classpath = sourceSets.integrationTest.runtimeClasspath
}

task functional(type: Test, description: 'Runs the functional tests.', group: 'Verification') {

  systemProperty "scenario", System.getProperty('scenario')

  testClassesDirs = sourceSets.functionalTest.output.classesDirs
  classpath = sourceSets.functionalTest.runtimeClasspath
  finalizedBy aggregate
}

task smoke(type: Test, description: 'Runs the smoke tests.', group: 'Verification') {
  testClassesDirs = sourceSets.smokeTest.output.classesDirs
  classpath = sourceSets.smokeTest.runtimeClasspath
  finalizedBy aggregate
}

task migratePostgresDatabase(type: org.flywaydb.gradle.task.FlywayMigrateTask) {
  baselineOnMigrate = true
  schemas = ['locrefdata']
  locations = ["filesystem:src/main/resources/db/migration","filesystem:src/main/resources/db/postgres"]
  if (project.hasProperty("dburl")) {
    url = "jdbc:postgresql://${dburl}"
  }
}

project.ext {
  pactVersion = getCheckedOutGitCommitHash()
}

task contract(type: Test) {
  description = 'Runs the consumer Pact tests'
  useJUnitPlatform()
  testClassesDirs = sourceSets.contractTest.output.classesDirs
  classpath = sourceSets.contractTest.runtimeClasspath
  include "uk/gov/hmcts/reform/lrdapi/**"

}

task runProviderPactVerification(type: Test) {
  useJUnitPlatform()
  testClassesDirs = sourceSets.contractTest.output.classesDirs
  classpath = sourceSets.contractTest.runtimeClasspath
  if (project.hasProperty('pact.verifier.publishResults')) {
    systemProperty 'pact.verifier.publishResults', project.property('pact.verifier.publishResults')
  }
  systemProperty 'pact.provider.version', project.pactVersion
  include "uk/gov/hmcts/reform/lrdapi/provider/**"
}

task fortifyScan(type: JavaExec)  {
  main("uk.gov.hmcts.fortifyclient.FortifyClientMainApp")
  classpath += sourceSets.test.runtimeClasspath
  jvmArgs = ['--add-opens=java.base/java.lang.reflect=ALL-UNNAMED']
  // This is a temporary fix to prevent the nightly build from failing if the Fortify scan detects issues
  ignoreExitValue = true as JavaExecSpec
}

runProviderPactVerification.dependsOn contract

runProviderPactVerification.finalizedBy pactVerify

def getCheckedOutGitCommitHash() {
  'git rev-parse --verify --short HEAD'.execute().text.trim()
}

pact {
  publish {
    pactDirectory = 'pacts'
    pactBrokerUrl = System.getenv("PACT_BROKER_FULL_URL") ?: 'http://localhost:9292'
    tags = [System.getenv("PACT_BRANCH_NAME") ?:'Dev']
    version = project.pactVersion
  }
}

jacoco {
  toolVersion = "0.8.13"
}

jacocoTestReport {
  executionData(test, integration)
  reports {
    xml.required = true
    csv.required = false
    xml.outputLocation = file("${project.buildDir}/reports/jacoco/test/jacocoTestReport.xml")
  }
}

pitest {
  junit5PluginVersion = versions.junitPitest
  targetClasses = ['uk.gov.hmcts.reform.lrdapi.*']
  excludedClasses = [
    'uk.gov.hmcts.reform.lrdapi.config.*',
    'uk.gov.hmcts.reform.lrdapi.controllers.*',
    'uk.gov.hmcts.reform.lrdapi.LrdApiApplication'
  ]
  threads = 10
  enableDefaultIncrementalAnalysis = true
  historyInputLocation = 'build/reports/pitest/fastermutationtesting'
  historyOutputLocation = 'build/reports/pitest/fastermutationtestingoutput'
  outputFormats = ['XML', 'HTML']
  timestampedReports = false
  mutationThreshold = 90
  useClasspathFile = true
}


project.tasks['pitest'].group = "Verification"

sonarqube {
  properties {
    property "sonar.projectName", "reform::rd-location-ref-api"
    property "sonar.projectKey", "uk.gov.hmcts.reform:reform::rd-location-ref-api"
    property "sonar.coverage.jacoco.xmlReportPaths", "${project.buildDir}/reports/jacoco/test/jacocoTestReport.xml"
    property "sonar.pitest.mode", "reuseReport"
    property "sonar.pitest.reportsDirectory", "build/reports/pitest"
    property "sonar.exclusions", "src/main/java/uk/gov/hmcts/reform/lrdapi/config/**," +
      "src/main/java/uk/gov/hmcts/reform/lrdapi/LrdApiApplication.java"
  }
}

project.tasks['sonarqube'].dependsOn test, integration, jacocoTestReport
project.tasks['sonarqube'].group = "Verification"

repositories {
  mavenLocal()
  mavenCentral()
  maven {
    url "https://jitpack.io"
  }
}

dependencies {

  implementation group: 'jakarta.validation', name: 'jakarta.validation-api', version: '3.1.1'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-actuator'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-aop'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-json'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-security'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-jdbc'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-cache'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-oauth2-client'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-oauth2-resource-server'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-validation'
  implementation group: 'org.springframework.retry', name: 'spring-retry', version: '2.0.11'
  implementation (group: 'io.rest-assured', name: 'rest-assured', version: '5.5.1') {
    exclude group: "com.sun.xml.bind", module: "jaxb-osgi"
    exclude group: "org.apache.sling"
  }
  implementation group: 'org.springdoc', name: 'springdoc-openapi-starter-webmvc-ui', version: '2.8.8'
  implementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-bootstrap'

  implementation group: 'com.github.ben-manes.caffeine', name: 'caffeine', version: '3.2.0'

  implementation group: 'com.github.hmcts', name: 'service-auth-provider-java-client', version: '5.3.2'

  implementation group: 'com.auth0', name: 'java-jwt', version: '4.5.0'
  implementation group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: '2.18.3'
  implementation group: 'io.jsonwebtoken', name: 'jjwt', version:'0.9.1'
  implementation group: 'com.fasterxml.jackson', name: 'jackson-bom', version: versions.jackson, ext: 'pom'
  implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: versions.jackson
  implementation group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: versions.jackson
  implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: versions.jackson
  implementation group: 'com.fasterxml.jackson', name: 'jackson-bom', version: '2.18.3', ext: 'pom'
  implementation group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: versions.jackson

  implementation group: 'jakarta.inject', name: 'jakarta.inject-api', version: '2.0.1'
  implementation 'com.github.hmcts:idam-java-client:3.0.4'

  implementation group: 'com.nimbusds', name: 'nimbus-jose-jwt', version: '9.48'
  implementation group: 'com.launchdarkly', name: 'launchdarkly-java-server-sdk', version: versions.launchDarklySdk

  implementation group: 'org.flywaydb', name: 'flyway-core', version: '9.22.3'
  implementation group: 'org.postgresql', name: 'postgresql', version: '42.7.5'

  implementation group: 'com.google.guava', name: 'guava', version: '33.4.8-jre'
  implementation group: 'jakarta.el', name: 'jakarta.el-api', version: '6.0.1'

  implementation group: 'org.bouncycastle', name: 'bcpkix-jdk18on', version: '1.80'
  implementation group: 'ch.qos.logback', name: 'logback-core', version: versions.logback
  implementation group: 'ch.qos.logback', name: 'logback-classic', version: versions.logback

  implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: versions.log4j
  implementation group: 'org.apache.logging.log4j', name: 'log4j', version: versions.log4j
  implementation group: 'org.apache.logging.log4j', name: 'log4j-to-slf4j', version: versions.log4j

  implementation group:"org.yaml", name: "snakeyaml", version: '2.4'

  implementation group: 'io.github.openfeign.form', name: 'feign-form', version: versions.feign
  implementation group: 'io.github.openfeign.form', name: 'feign-form-spring', version: versions.feign
  implementation 'io.github.openfeign:feign-httpclient:13.5'
  implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: versions.log4j
  implementation "com.github.hmcts.java-logging:logging:${versions.reformLogging}"
  implementation "com.github.hmcts.java-logging:logging-appinsights:5.1.9"
  implementation group: 'org.apache.commons', name: 'commons-lang3', version: versions.commonsLang3

  testImplementation 'com.github.hmcts:rd-commons-lib:0.1.0'

  compileOnly group: 'org.projectlombok', name: 'lombok', version: versions.lombok
  annotationProcessor group: 'org.projectlombok', name: 'lombok', version: versions.lombok
  testCompileOnly group: 'org.projectlombok', name: 'lombok', version: versions.lombok
  testAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: versions.lombok
  integrationTestCompileOnly group: 'org.projectlombok', name: 'lombok', version: versions.lombok
  integrationTestAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: versions.lombok
  functionalTestCompileOnly group: 'org.projectlombok', name: 'lombok', version: versions.lombok
  functionalTestAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: versions.lombok
  smokeTestCompileOnly group: 'org.projectlombok', name: 'lombok', version: versions.lombok
  smokeTestAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: versions.lombok

  testImplementation("org.hamcrest:hamcrest-junit:2.0.0.0") {
    exclude group: "org.hamcrest", module: "hamcrest-core"
    exclude group: "org.hamcrest", module: "hamcrest-library"
  }

  testImplementation group: 'io.rest-assured', name: 'rest-assured', version: '5.5.1'
  testImplementation group: 'io.rest-assured', name: 'rest-assured-common', version: '5.5.1'

  testImplementation("com.github.tomakehurst:wiremock-jre8-standalone:3.0.1")

  testImplementation ('org.mockito:mockito-core:5.16.1') {
    exclude group: "net.bytebuddy", module: "byte-buddy"
    exclude group: "net.bytebuddy", module: "byte-buddy-agent"
  }
  testImplementation ('org.mockito:mockito-inline:5.2.0') {
    exclude group: "net.bytebuddy", module: "byte-buddy"
    exclude group: "net.bytebuddy", module: "byte-buddy-agent"
  }
  testImplementation group: 'net.bytebuddy', name: 'byte-buddy', version: '1.17.5'
  testImplementation group: 'net.bytebuddy', name: 'byte-buddy-agent', version: '1.17.5'

  testImplementation group: 'org.mockito', name: 'mockito-junit-jupiter', version: '5.16.1'
  testImplementation group: 'org.powermock', name: 'powermock-api-mockito2', version: '2.0.9'

  testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test', {
    exclude group: 'junit', module: 'junit'
    exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
  }
  testImplementation 'io.github.openfeign:feign-jackson:13.5'
  testImplementation group: 'com.github.mifmif', name: 'generex', version: '1.0.2'

  testImplementation group: 'org.codehaus.sonar-plugins', name: 'sonar-pitest-plugin', version: versions.sonarPitest

  testImplementation 'org.springframework.cloud:spring-cloud-contract-wiremock:4.2.1'

  testImplementation group: 'org.testcontainers', name: 'postgresql', version: versions.testContainer_postgresql
  testImplementation group: 'org.testcontainers', name: 'junit-jupiter', version: versions.testContainer_postgresql
  testImplementation group: 'org.testcontainers', name: 'testcontainers', version: versions.testContainer_postgresql
  testImplementation group: 'org.skyscreamer', name: 'jsonassert', version: '1.5.3'

  testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: versions.junit
  testImplementation group: 'net.serenity-bdd', name: 'serenity-rest-assured', version: versions.serenity
  testImplementation group: 'net.serenity-bdd', name: 'serenity-junit', version: versions.serenity

  functionalTestImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: versions.junit
  functionalTestRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: versions.junit
  functionalTestImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: versions.junit
  functionalTestImplementation group: 'org.junit.platform', name: 'junit-platform-commons', version: versions.junitPlatform
  functionalTestRuntimeOnly group: 'org.junit.platform', name: 'junit-platform-engine', version: versions.junitPlatform

  smokeTestImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: versions.junit
  smokeTestRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: versions.junit
  smokeTestImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: versions.junit
  smokeTestImplementation group: 'org.junit.platform', name: 'junit-platform-commons', version: versions.junitPlatform
  smokeTestRuntimeOnly group: 'org.junit.platform', name: 'junit-platform-engine', version: versions.junitPlatform

  integrationTestImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: versions.junit
  integrationTestRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: versions.junit
  integrationTestImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: versions.junit
  integrationTestImplementation group: 'org.junit.platform', name: 'junit-platform-commons', version: versions.junitPlatform
  integrationTestRuntimeOnly group: 'org.junit.platform', name: 'junit-platform-engine', version: versions.junitPlatform

  contractTestImplementation group: 'au.com.dius.pact.consumer', name: 'junit5', version: versions.pact_version
  contractTestRuntimeOnly group: 'au.com.dius.pact.consumer', name: 'junit5', version: versions.pact_version

  contractTestImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: versions.junit
  contractTestRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: versions.junit
  contractTestImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: versions.junit
  contractTestImplementation group: 'au.com.dius.pact.provider', name: 'junit5', version: versions.pact_version
  contractTestImplementation group: 'au.com.dius.pact.provider', name: 'spring', version: versions.pact_version
  contractTestImplementation group: 'au.com.dius.pact.provider', name: 'junit5spring', version: versions.pact_version

  integrationTestImplementation sourceSets.main.runtimeClasspath
  integrationTestImplementation sourceSets.test.runtimeClasspath


  functionalTestImplementation sourceSets.main.runtimeClasspath
  functionalTestImplementation sourceSets.test.runtimeClasspath

  smokeTestImplementation sourceSets.main.runtimeClasspath
  smokeTestImplementation sourceSets.test.runtimeClasspath

  contractTestImplementation sourceSets.main.runtimeClasspath
  contractTestImplementation sourceSets.test.runtimeClasspath

  pactTestImplementation sourceSets.main.runtimeClasspath
  pactTestImplementation sourceSets.test.runtimeClasspath
}

dependencyManagement {
  imports {
    mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
  }
}

dependencyCheck {
  suppressionFile = 'config/owasp/suppressions.xml'
}

dependencyUpdates.resolutionStrategy = {
  componentSelection { rules ->
    rules.all { ComponentSelection selection ->
      boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
        selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.d-]*/
      }
      if (rejected) {
        selection.reject('Release candidate')
      }
    }
  }
}

gradle.startParameter.continueOnFailure = true

bootJar {
  archiveFileName = jarName
  manifest {
    attributes('Implementation-Version': project.version.toString())
  }
}

rootProject.tasks.named("processFunctionalTestResources") {
  duplicatesStrategy = 'include'
}

rootProject.tasks.named("processIntegrationTestResources") {
  duplicatesStrategy = 'include'
}

